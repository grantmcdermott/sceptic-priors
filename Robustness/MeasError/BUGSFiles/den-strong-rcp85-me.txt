model{
    
    for(t in 1:N) {
    mu[t] <- alpha + beta*trf[t] + gamma*volc_mean[t] + delta*soi_mean[t] + eta*amo_mean[t]
    
    ## Meas. error in variance of y, i.e.: y[t]* = y[t] + v[t], where Var(v[t]) = omega[t]
    ## Note change in variance and tau formula because of combined normal distributions
    ## See: https://en.wikipedia.org/wiki/Sum_of_normally_distributed_random_variables
    
    sigmasq_tot[t] <- pow(sigma, 2) + pow(had_omega[t], 2)
    tau_tot[t] <- pow(sigmasq_tot[t], -1)
    
    had[t]  ~ dnorm(mu[t], tau_tot[t])
    y_pred[t] ~ dnorm(mu[t], tau_tot[t]) 
    }
     
      ## Prior on beta: One of four subjective prior, conviction combinations...
      
      # 1) Mod. lukewarmer: TCR ~ N(1, 0.25^2). Mean of 1 °C and uncertainty range of 1.0 °C (95% probability).
      #    Converting to beta (= TCR/3.71): B ~ N( 1/3.71, (0.25/3.71)^2 )
      # 2) Strong lukewarmer: TCR ~ N(1, 0.065^2). Mean of 1 °C and uncertainty range of 0.25 °C (95% probability).
      #    Converting to beta (= TCR/3.71): B ~ N( 1/3.71, (0.065/3.71)^2 )
      # 3) Mod. denier: TCR ~ N(0, 0.25^2). Mean of 0 °C and uncertainty range of 1.0 °C (95% probability).
      #    Converting to beta (= TCR/3.71): B ~ N( 0/3.71, (0.25/3.71)^2 )
      # 4) Strong denier: TCR ~ N(0, 0.065^2). Mean of 0 °C and uncertainty range of 0.25 °C (95% probability).
      #    Converting to beta (= TCR/3.71): B ~ N( 0/3.71, (0.065/3.71)^2 )
      
        mu_beta <- 0
        sigma_beta <- 0.065/3.71
       
    ## Priors for all parameters   
    alpha ~ dnorm(0, 0.0001)            ## intercept
    
    tau_beta <- pow(sigma_beta, -2)
    beta ~ dnorm(mu_beta, tau_beta)     ## trf coef
    
    gamma ~ dnorm(0, 0.0001)            ## volc coef
    delta ~ dnorm(0, 0.0001)            ## soi coef
    eta ~ dnorm(0, 0.0001)              ## amo coef
    sigma ~ dunif(0, 100)               ## Residual std dev
    #tau <- pow(sigma, -2) ## Changed to commented out
    
    had0 ~ dnorm(0.0, 1.0E-6)           ## Initialising value
    }
